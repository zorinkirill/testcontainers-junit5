plugins {
    id "idea"
    id "jacoco"
    id "com.github.kt3k.coveralls"
    id "io.github.gradle-nexus.publish-plugin"
}

jacoco {
    toolVersion = "$jacocoToolVersion"
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
    }
}

nexusPublishing {
    repositories {
        sonatype() {
            nexusUrl = uri("https://s01.oss.sonatype.org/service/local/")
            snapshotRepositoryUrl = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
        }
    }
}

task jacocoRootReport(type: JacocoReport, group: 'Coverage reports') {
    group = LifecycleBasePlugin.VERIFICATION_GROUP

    subprojects {
        plugins.withType(JacocoPlugin) {
            tasks.matching { it.extensions.findByType(JacocoTaskExtension) != null }.all { Task task ->
                sourceSets(sourceSets.main)
                classDirectories.setFrom files(classDirectories.files.collect {
                    fileTree(dir: it)
                })
                executionData(task)
            }
        }
    }

    reports {
        csv.required = false
        xml.required = true
        html.required = true
    }

    onlyIf = {
        true
    }
}

coveralls {
    jacocoReportPath "build/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
}

wrapper {
    gradleVersion = "7.1"
}
